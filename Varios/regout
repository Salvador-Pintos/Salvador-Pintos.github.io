 /*******************************************************************/
 /*                                                                 */
 /*  EJEMPLO SOBRE COMO SACAR EL PRONOSTICO DE UN MODELO            */
 /*  CON LA SENTENCIA OUTPUT  OUT=                                  */
 /*  PARA POSTERIORMENTE DEVOLVER LA TRANSFORMACION                 */
 /*  LOGARITMICA HECHA SOBRE LA DATA ORIGINAL DE POBLACION          */
 /*                                                                 */
 /*                                                                 */
 /*******************************************************************/
   data uspobla;
      input pobla @@;
      retain year 1780;
      year=year+10;
      yearcuad=year*year;
      pobla=pobla/1000;
      z=log(pobla);
      cards;
   3929 5308 7239 9638 12866 17069 23191 31443 39818 50155
   62947 75994 91972 105710 122775 131669 151325 179323 203211
   ;
   proc reg data=uspobla ;
     model z=year /p r;
   output out=salida p=zpredict l95=zinf95 u95=zsup95;
   data final;
   set salida;
   keep year   predict pobla sup95 inf95;
    predict=exp(zpredict);
    sup95=exp(zsup95);
    inf95=exp(zinf95);
    proc print;
id year;
run;
